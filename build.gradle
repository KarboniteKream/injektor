plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.kream'
version '0.0.2-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api 'javax.inject:javax.inject:1'

    compileOnly 'org.jetbrains:annotations:19.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.assertj:assertj-core:3.16.1'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java

            pom {
                name = 'injektor'
                description = 'A simple dependency injection container for Java.'
                url = 'https://github.com/KarboniteKream/injektor'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        name = 'Klemen Ko≈°ir'
                        email = 'klemen.kosir@kream.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/KarboniteKream/injektor.git'
                    developerConnection = 'scm:git:ssh://github.com:KarboniteKream/injektor.git'
                    url = 'https://github.com/KarboniteKream/injektor'
                }
            }
        }
    }
    repositories {
        maven {
            def snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            def releaseUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

            url = version.endsWith('-SNAPSHOT') ? snapshotUrl : releaseUrl
            credentials {
                username project.findProperty('ossrh.username')
                password project.findProperty('ossrh.password')
            }
        }
    }
}

signing {
    sign publishing.publications.jar
}
